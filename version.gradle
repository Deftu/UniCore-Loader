buildscript {
    def mcVersionStr = project.name.tokenize("-")[0]
    def (major, minor, patch) = mcVersionStr.tokenize(".")
    def base = major == null || minor == null
    def mcVersion = base ? 11800 : "${major}${minor.padLeft(2, "0")}${(patch ?: "").padLeft(2, "0")}" as int
    def fabric = mcVersion >= 11400
    project.ext.mcVersion = mcVersion
    project.ext.mcVersionStr = mcVersionStr
    project.ext.mcPlatform = fabric ? "fabric" : "forge"
    project.ext.fabric = fabric
    project.ext.forge = !fabric

    repositories {
        maven { url("https://jitpack.io/") }
        maven { url("https://maven.architectury.dev/") }
        maven { url("https://maven.fabricmc.net") }
        maven { url("https://maven.minecraftforge.net") }
        maven { url("https://repo.sk1er.club/repository/maven-public/") }
    }

    dependencies {
        classpath("gg.essential:architectury-loom:0.10.0.0")
        if (!fabric) classpath("dev.architectury.architectury-pack200:dev.architectury.architectury-pack200.gradle.plugin:0.1.3")
    }
}

def FABRIC = fabric
def FORGE = !FABRIC

if (FORGE) apply plugin: "dev.architectury.architectury-pack200"

apply plugin: "gg.essential.loom"
apply plugin: "maven-publish"
apply plugin: "java"

apply plugin: "com.replaymod.preprocess"
preprocess {
    vars.put("MC", mcVersion)
    vars.put("FABRIC", project.fabric ? 1 : 0)
    vars.put("FORGE", project.fabric ? 0 : 1)
}

group = projectGroup
archivesBaseName = "UniCore-Loader-Stage0-${mcVersionStr}-${mcPlatform}"
version = projectVersion

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(mcVersion >= 11800 ? 17 : mcVersion >= 11700 ? 16 : 8))
    }
}

loom {
    runConfigs {
        client {
            ideConfigGenerated = true
        }
    }

    if (FORGE) {
        forge {
            pack200Provider = new dev.architectury.pack200.java.Pack200Adapter()
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:" + [
            11800: "1.18",
            10809: "1.8.9"
    ][mcVersion])
    mappings([
            11800: "net.fabricmc:yarn:1.18+build.1:v2",
            10809: "de.oceanlabs.mcp:mcp_stable:22-1.8.9"
    ][mcVersion])
    if (FORGE) forge("net.minecraftforge:forge:1.8.9-11.15.1.2318-1.8.9")
}

java {
    withJavadocJar()
}

artifacts {
    archives(javadocJar)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "UniCore-Loader-${mcVersionStr}-${mcPlatform}"

            from components.java
        }
    }
}